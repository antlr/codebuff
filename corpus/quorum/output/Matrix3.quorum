package Libraries.Compute

use Libraries.Compute.all
use Libraries.Containers.Array
use Libraries.Language.Errors.MatrixError

/*
Matrix3 is a class representing a 3x3 matrix.

Attribute: Author Amanda Austin

Attribute: Example

use Libraries.Compute.Matrix3

Matrix3 matrix

matrix:Set(2, 5, 0, 1, 3, 0, 0, 0, 1)
matrix:Inverse()

number row0column0 = matrix:row0column0
number row0column1 = matrix:row0column1
number row0column2 = matrix:row0column2
number row1column0 = matrix:row1column0
number row1column1 = matrix:row1column1
number row1column2 = matrix:row1column2
number row2column0 = matrix:row2column0
number row2column1 = matrix:row2column1
number row2column2 = matrix:row2column2

output "The new matrix is:"
output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

*/


class Matrix3
    public number row0column0 = 1
    public number row0column1 = 0
    public number row0column2 = 0
    public number row1column0 = 0
    public number row1column1 = 1
    public number row1column2 = 0
    public number row2column0 = 0
    public number row2column1 = 0
    public number row2column2 = 1
    Math math

    /*
    This action sets the matrix to the identity matrix.

    Attribute: Author Amanda Austin

    Attribute: Returns The matrix as the identity matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    matrix:IdentityMatrix()

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action IdentityMatrix returns Matrix3
        row0column0 = 1
        row0column1 = 0
        row0column2 = 0
        row1column0 = 0
        row1column1 = 1
        row1column2 = 0
        row2column0 = 0
        row2column1 = 0
        row2column2 = 1

        return me
    end

    /*
    This action performs the matrix multiplication matrixA * matrixB. The result
    is stored in the calling matrix. Calling C:Multiply(A, B) results in C = AB.

    Attribute: Author Amanda Austin

    Attribute: Parameter matrixA The left hand matrix to multiply
    Attribute: Parameter matrixB The right hand matrix to multiply

    Attribute: Returns The product of the two matrices

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrixA
    Matrix3 matrixB
    Matrix3 product

    matrixA:Set(0, 2, 4, 6, 8, 10, 12, 14, 16)
    matrixB:Set(1, 3, 5, 7, 9, 11, 13, 15, 17)

    product:Multiply(matrixA, matrixB)

    number row0column0 = product:row0column0
    number row0column1 = product:row0column1
    number row0column2 = product:row0column2
    number row1column0 = product:row1column0
    number row1column1 = product:row1column1
    number row1column2 = product:row1column2
    number row2column0 = product:row2column0
    number row2column1 = product:row2column1
    number row2column2 = product:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Multiply(Matrix3 matrixA, Matrix3 matrixB) returns Matrix3
        number v00 = matrixA:row0column0 * matrixB:row0column0 + matrixA:row0column1 * matrixB:row1column0 + matrixA:row0column2 * matrixB:row2column0
        number v01 = matrixA:row0column0 * matrixB:row0column1 + matrixA:row0column1 * matrixB:row1column1 + matrixA:row0column2 * matrixB:row2column1
        number v02 = matrixA:row0column0 * matrixB:row0column2 + matrixA:row0column1 * matrixB:row1column2 + matrixA:row0column2 * matrixB:row2column2
        number v10 = matrixA:row1column0 * matrixB:row0column0 + matrixA:row1column1 * matrixB:row1column0 + matrixA:row1column2 * matrixB:row2column0
        number v11 = matrixA:row1column0 * matrixB:row0column1 + matrixA:row1column1 * matrixB:row1column1 + matrixA:row1column2 * matrixB:row2column1
        number v12 = matrixA:row1column0 * matrixB:row0column2 + matrixA:row1column1 * matrixB:row1column2 + matrixA:row1column2 * matrixB:row2column2
        number v20 = matrixA:row2column0 * matrixB:row0column0 + matrixA:row2column1 * matrixB:row1column0 + matrixA:row2column2 * matrixB:row2column0
        number v21 = matrixA:row2column0 * matrixB:row0column1 + matrixA:row2column1 * matrixB:row1column1 + matrixA:row2column2 * matrixB:row2column1
        number v22 = matrixA:row2column0 * matrixB:row0column2 + matrixA:row2column1 * matrixB:row1column2 + matrixA:row2column2 * matrixB:row2column2
        row0column0 = v00
        row0column1 = v01
        row0column2 = v02
        row1column0 = v10
        row1column1 = v11
        row1column2 = v12
        row2column0 = v20
        row2column1 = v21
        row2column2 = v22

        return me
    end
 
    /*
    This action multiplies the calling matrix by the passed matrix and stores
    the result in the calling matrix. Calling A:Multiply(B) results in A = AB.

    Attribute: Author Amanda Austin

    Attribute: Parameter matrix The matrix to multiply by

    Attribute: Returns The calling matrix after multiplication

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrixA
    Matrix3 matrixB

    matrixA:Set(0, 2, 4, 6, 8, 10, 12, 14, 16)
    matrixB:Set(1, 3, 5, 7, 9, 11, 13, 15, 17)

    matrixA:Multiply(matrixB)

    number row0column0 = matrixA:row0column0
    number row0column1 = matrixA:row0column1
    number row0column2 = matrixA:row0column2
    number row1column0 = matrixA:row1column0
    number row1column1 = matrixA:row1column1
    number row1column2 = matrixA:row1column2
    number row2column0 = matrixA:row2column0
    number row2column1 = matrixA:row2column1
    number row2column2 = matrixA:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action Multiply(Matrix3 matrix) returns Matrix3
        number v00 = row0column0 * matrix:row0column0 + row0column1 * matrix:row1column0 + row0column2 * matrix:row2column0
        number v01 = row0column0 * matrix:row0column1 + row0column1 * matrix:row1column1 + row0column2 * matrix:row2column1
        number v02 = row0column0 * matrix:row0column2 + row0column1 * matrix:row1column2 + row0column2 * matrix:row2column2
        number v10 = row1column0 * matrix:row0column0 + row1column1 * matrix:row1column0 + row1column2 * matrix:row2column0
        number v11 = row1column0 * matrix:row0column1 + row1column1 * matrix:row1column1 + row1column2 * matrix:row2column1
        number v12 = row1column0 * matrix:row0column2 + row1column1 * matrix:row1column2 + row1column2 * matrix:row2column2
        number v20 = row2column0 * matrix:row0column0 + row2column1 * matrix:row1column0 + row2column2 * matrix:row2column0
        number v21 = row2column0 * matrix:row0column1 + row2column1 * matrix:row1column1 + row2column2 * matrix:row2column1
        number v22 = row2column0 * matrix:row0column2 + row2column1 * matrix:row1column2 + row2column2 * matrix:row2column2
        row0column0 = v00
        row0column1 = v01
        row0column2 = v02
        row1column0 = v10
        row1column1 = v11
        row1column2 = v12
        row2column0 = v20
        row2column1 = v21
        row2column2 = v22

        return me
    end

    /*
    This action sets the matrix to the rotation matrix for the passed angle in
    degrees counter-clockwise. A rotation matrix is often used in graphics
    programming to represent rotations.

    Attribute: Author Amanda Austin

    Attribute: Parameter degrees The angle in degrees

    Attribute: Returns The matrix as a rotation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:SetToRotation(45)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action SetToRotation(number degrees) returns Matrix3
        return SetToRotationInRadians(math:DegreesToRadians(degrees))
    end

    /*
    This action sets the matrix to the rotation matrix for the passed angle in
    degrees counter-clockwise about the passed axis. A rotation matrix is often 
    used in graphics programming to represent rotations.

    Attribute: Author Amanda Austin

    Attribute: Parameter axis The rotation axis
    Attribute: Parameter degrees The angle in degrees

    Attribute: Returns The matrix as a rotation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector3

    Matrix3 matrix
    Vector3 axis
    
    axis:Set(0, 0, 1)
    matrix:SetToRotation(axis, 45)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action SetToRotation(Vector3 axis, number degrees) returns Matrix3
        return SetToRotation(axis, math:Cosine(math:DegreesToRadians(degrees)), math:Sine(math:DegreesToRadians(degrees)))
    end

    /*
    This action sets the matrix to a rotation matrix about the passed axis using
    the cosine and sine of the angle of rotation. A rotation matrix is often
    used in graphics programming to represent rotations.

    Attribute: Author Amanda Austin

    Attribute: Parameter axis The rotation axis
    Attribute: Parameter cosine The cosine of the angle
    Attribute: Parameter sine The sine of the angle

    Attribute: Returns The matrix as a rotation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector3
    use Libraries.Compute.Math

    Math math

    Matrix3 matrix
    Vector3 axis

    number cosine = math:SquareRoot(2) / 2
    number sine = math:SquareRoot(2) / 2

    axis:Set(0, 0, 1)

    matrix:SetToRotation(axis, cosine, sine)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetToRotation(Vector3 axis, number cosine, number sine) returns Matrix3
        number oc = 1.0 - cosine
        row0column0 = oc * axis:GetX() * axis:GetX() + cosine
        row0column1 = oc * axis:GetX() * axis:GetY() - axis:GetZ() * sine
        row0column2 = oc * axis:GetZ() * axis:GetX() + axis:GetY() * sine
        row1column0 = oc * axis:GetX() * axis:GetY() + axis:GetZ() * sine
        row1column1 = oc * axis:GetY() * axis:GetY() + cosine
        row1column2 = oc * axis:GetY() * axis:GetZ() - axis:GetX() * sine
        row2column0 = oc * axis:GetZ() * axis:GetX() - axis:GetY() * sine
        row2column1 = oc * axis:GetY() * axis:GetZ() + axis:GetX() * sine
        row2column2 = oc * axis:GetZ() * axis:GetZ() + cosine

        return me
    end

    /*
    This action sets the matrix to the rotation matrix for the passed angle in
    radians. A rotation matrix is often used in graphics programming to 
    represent rotations.

    Attribute: Author Amanda Austin

    Attribute: Parameter radians The angle in radians

    Attribute: Returns The matrix as a rotation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Math

    Math math

    Matrix3 matrix

    matrix:SetToRotationInRadians(math:pi / 4)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action SetToRotationInRadians(number radians) returns Matrix3
        number cosine = math:Cosine(radians)
        number sine = math:Sine(radians)
        row0column0 = cosine
        row1column0 = sine
        row2column0 = 0
        row0column1 = sine * -1
        row1column1 = cosine
        row2column1 = 0
        row0column2 = 0
        row1column2 = 0
        row2column2 = 1

        return me
    end
        
    /*
    This action sets the matrix to a translation matrix by the given x and y
    values. A translation matrix is often used in graphics programming to 
    represent translations.

    Attribute: Author Amanda Austin

    Attribute: Parameter x The x value of the translation
    Attribute: Parameter y The y value of the translation

    Attribute: Returns The matrix as a translation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:SetToTranslation(4, 5)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action SetToTranslation(number x, number y) returns Matrix3
        row0column0 = 1
        row1column0 = 0
        row2column0 = 0
        row0column1 = 0
        row1column1 = 1
        row2column1 = 0
        row0column2 = x
        row1column2 = y
        row2column2 = 1

        return me
    end

    /*
    This action sets the matrix to a translation matrix by the given vector
    representing the translation. A translation matrix is often used in graphics
    programming to represent translations.

    Attribute: Author Amanda Austin
    
    Attribute: Parameter translation The translation vector

    Attribute: Returns The matrix as a translation matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 translation

    translation:Set(4, 5)

    matrix:SetToTranslation(translation)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetToTranslation(Vector2 translation) returns Matrix3
        row0column0 = 1
        row1column0 = 0
        row2column0 = 0
        row0column1 = 0
        row1column1 = 1
        row2column1 = 0
        row0column2 = translation:GetX()
        row1column2 = translation:GetY()
        row2column2 = 1

        return me
    end

    /*
    This action ses the matrix to a scaling matrix with the passed scales in the
    x and y directions. A scaling matrix is often used in graphics programming
    to represent a scale.

    Attribute: Author Amanda Austin

    Attribute: Parameter scaleX The scale in the x-direction
    Attribute: Parameter scaleY The scale in the y-direction

    Attribute: Returns The matrix as a scale matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:SetToScaling(2, 2)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetToScaling(number scaleX, number scaleY) returns Matrix3
        row0column0 = scaleX
        row0column1 = 0
        row0column2 = 0
        row1column0 = 0
        row1column1 = scaleY
        row1column2 = 0
        row2column0 = 0
        row2column1 = 0
        row2column2 = 1

        return me
    end

    /*
    This action ses the matrix to a scaling matrix with the passed vector as the
    scale vector. A scaling matrix is often used in graphics programming
    to represent a scale.

    Attribute: Author Amanda Austin

    Attribute: Parameter scale The vector representing the scale

    Attribute: Returns The matrix as a scale matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 scale

    scale:Set(2, 2)

    matrix:SetToScaling(scale)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetToScaling(Vector2 scale) returns Matrix3
        row0column0 = scale:GetX()
        row0column1 = 0
        row0column2 = 0
        row1column0 = 0
        row1column1 = scale:GetY()
        row1column2 = 0
        row2column0 = 0
        row2column1 = 0
        row2column2 = 1

        return me
    end

    /*
    This action computes the determinant of the matrix.

    Attribute: Example Amanda Austin

    Attribute: Returns The determinant of the matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:Set(2, 5, 0, 1, 3, 0, 0, 0, 1)

    number determinant = matrix:Determinant()

    output "The determinant of the matrix is " + determinant

    */


    action Determinant returns number
        return row0column0 * row1column1 * row2column2 + row0column1 * row1column2 * row2column0 + row0column2 * row1column0 * row2column1 - row0column0 * row1column2 * row2column1 - row0column1 * row1column0 * row2column2 - row0column2 * row1column1 * row2column0
    end

    /*
    This action computes the inverse of the matrix.

    Attribute: Author Amanda Austin

    Attribute: Returns The inverse of the matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:Set(2, 5, 0, 1, 3, 0, 0, 0, 1)

    matrix:Inverse()

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action Inverse returns Matrix3
        number determinant = Determinant()
        if determinant = 0
            MatrixError matrix
            matrix:SetErrorMessage("Matrix is not invertable if the determinant computes to 0.")
            alert(matrix)
        end

        number inverseDeterminant = 1.0 / determinant
        t00 = row1column1 * row2column2 - row2column1 * row1column2
        t10 = row2column0 * row1column2 - row1column0 * row2column2
        t20 = row1column0 * row2column1 - row2column0 * row1column1
        t01 = row2column1 * row0column2 - row0column1 * row2column2
        t11 = row0column0 * row2column2 - row2column0 * row0column2
        t21 = row2column0 * row0column1 - row0column0 * row2column1
        t02 = row0column1 * row1column2 - row1column1 * row0column2
        t12 = row1column0 * row0column2 - row0column0 * row1column2
        t22 = row0column0 * row1column1 - row1column0 * row0column1
        row0column0 = inverseDeterminant * t00
        row1column0 = inverseDeterminant * t10
        row2column0 = inverseDeterminant * t20
        row0column1 = inverseDeterminant * t01
        row1column1 = inverseDeterminant * t11
        row2column1 = inverseDeterminant * t21
        row0column2 = inverseDeterminant * t02
        row1column2 = inverseDeterminant * t12
        row2column2 = inverseDeterminant * t22

        return me
    end


    /*
    This action sets the values of the matrix using the values of the passed
    array. The values of the array are used in order to set the matrix values
    row by row. For example, the first element in the array is used to set the
    cell of the matrix in the first row and first column, the second element is
    used to set the cell of the matrix in the first row and second column, the
    third element for the first row and third column, the fourth element for the
    second row and first column, etc.

    Attribute: Author Amanda Austin

    Attribute: Parameter array The array that contains the values to set as the matrix values

    Attribute: Returns The matrix with the values from the array
    
    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Containers.Array

    Matrix3 matrix
    Array<number> array

    integer i = 0
    repeat 9 times
        array:Add(i)
        i = i + 1
    end

    matrix:Set(array)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Set(Array<number> array) returns Matrix3
        if array:GetSize() not= 9
            MatrixError matrix
            matrix:SetErrorMessage("The array must have exactly 9 values to be converted to a Matrix3 object.")
            alert(matrix)
        end
        row0column0 = array:Get(0)
        row0column1 = array:Get(1)
        row0column2 = array:Get(2)
        row1column0 = array:Get(3)
        row1column1 = array:Get(4)
        row1column2 = array:Get(5)
        row2column0 = array:Get(6)
        row2column1 = array:Get(7)
        row2column2 = array:Get(8)
        return me
    end

    /*
    This action sets the values of the array to the passed values.

    Attribute: Author Amanda Austin

    Attribute: Parameter row0column0 The value of the first row, first column
    Attribute: Parameter row0column1 The value of the first row, second column
    Attribute: Parameter row0column2 The value of the first row, third column
    Attribute: Parameter row1column0 The value of the second row, first column
    Attribute: Parameter row1column1 The value of the second row, second column
    Attribute: Parameter row1column2 The value of the second row, third column
    Attribute: Parameter row2column0 The value of the third row, first column
    Attribute: Parameter row2column1 The value of the third row, second column
    Attribute: Parameter row2column2 The value of the third row, third column

    Attribute: Returns The matrix with the passed values

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    matrix:Set(0, 1, 2, 3, 4, 5, 6, 7, 8)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Set(number row0column0, number row0column1, number row0column2, number row1column0, number row1column1, number row1column2, number row2column0, number row2column1, number row2column2) returns Matrix3
        me:row0column0 = row0column0
        me:row0column1 = row0column1
        me:row0column2 = row0column2
        me:row1column0 = row1column0
        me:row1column1 = row1column1
        me:row1column2 = row1column2
        me:row2column0 = row2column0
        me:row2column1 = row2column1
        me:row2column2 = row2column2

        return me
    end

    /*
    This action sets the matrix to be a copy of the passed matrix.

    Attribute: Author Amanda Austin

    Attribute: Parameter matrix The matrix to set this matrix as

    Attribute: Returns The matrix with the values from the other matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    Matrix3 copyMatrix

    copyMatrix:Set(8, 7, 6, 5, 4, 3, 2, 1, 0)
    matrix:Set(copyMatrix)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Set(Matrix3 matrix) returns Matrix3
        row0column0 = matrix:row0column0
        row0column1 = matrix:row0column1
        row0column2 = matrix:row0column2
        row1column0 = matrix:row1column0
        row1column1 = matrix:row1column1
        row1column2 = matrix:row1column2
        row2column0 = matrix:row2column0
        row2column1 = matrix:row2column1
        row2column2 = matrix:row2column2

        return me
    end

    /*
    This action creates and returns a new matrix that is a copy of the calling
    matrix

    Attribute: Author Amanda Austin

    Attribute: Returns A new matrix that is a copy of this matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    Matrix3 copyMatrix

    copyMatrix:Set(8, 7, 6, 5, 4, 3, 2, 1, 0)
    matrix = copyMatrix:Copy()

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Copy returns Matrix3
        Matrix3 matrix
        matrix:row0column0 = row0column0
        matrix:row0column1 = row0column1
        matrix:row0column2 = row0column2
        matrix:row1column0 = row1column0
        matrix:row1column1 = row1column1
        matrix:row1column2 = row1column2
        matrix:row2column0 = row2column0
        matrix:row2column1 = row2column1
        matrix:row2column2 = row2column2

        return matrix
    end

    /*
    This action sets the matrix using the values of the passed Affine2. An
    Affine2 is essentially a 3-by-3 matrix used to represent transformations
    where the last row is always (0, 0, 1).

    Attribute: Author Amanda Austin

    Attribute: Parameter affine The affine to use to set the matrix

    Attribute: Returns The matrix with the values from the affine

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Affine2

    Matrix3 matrix
    Affine2 affine

    affine:Identity()
    
    matrix:Set(affine)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Set(Affine2 affine) returns Matrix3
        row0column0 = affine:row0column0
        row1column0 = affine:row1column0
        row2column0 = 0
        row0column1 = affine:row0column1
        row1column1 = affine:row1column1
        row2column1 = 0
        row0column2 = affine:row0column2
        row1column2 = affine:row1column2
        row2column2 = 1

        return me
    end

    /*
    This action sets the matrix to the values of the passed 4-by-4 matrix. The
    last row and last column of the passed 4-by-4 matrix are not used to set
    this matrix.

    Attribute: Author Amanda Austin

    Attribute: Parameter matrix The 4-by-4 matrix used to set the values of this matrix
    
    Attribute: Returns The matrix with the values from the 4-by-4 matrix

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Matrix4

    Matrix3 matrix
    Matrix4 biggerMatrix

    biggerMatrix:Set(0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15)

    matrix:Set(biggerMatrix)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Set(Matrix4 matrix) returns Matrix3
        row0column0 = matrix:row0column0
        row1column0 = matrix:row1column0
        row2column0 = matrix:row2column0
        row0column1 = matrix:row0column1
        row1column1 = matrix:row1column1
        row2column1 = matrix:row2column1
        row0column2 = matrix:row0column2
        row1column2 = matrix:row1column2
        row2column2 = matrix:row2column2

        return me
    end


    /*
    This action adds a translational component to the matrix. This action is
    mainly used in graphics programming to represent translations.

    Attribute: Author Amanda Austin

    Attribute: Parameter vector The vector to use as the translational component
    
    Attribute: Returns The matrix with the translational component
    
    Attribute: Example
    
    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 translation

    matrix:IdentityMatrix()
    translation:Set(4, 5)

    matrix:AddTranslationalComponent(translation)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action AddTranslationalComponent(Vector2 vector) returns Matrix3
        row0column2 = row0column2 + vector:GetX()
        row1column2 = row1column2 + vector:GetY()
        return me
    end

    /*
    This action adds a translational component to the matrix. This action is
    mainly used in graphics programming to represent translations.

    Attribute: Author Amanda Austin

    Attribute: Parameter x The x component of the translation
    Attribute: Parameter y The y component of the translation
    
    Attribute: Returns The matrix with the translational component
    
    Attribute: Example
    
    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:IdentityMatrix()

    matrix:AddTranslationalComponent(4, 5)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"
    
    */


    action AddTranslationalComponent(number x, number y) returns Matrix3
        row0column2 = row0column2 + x
        row1column2 = row1column2 + y

        return me
    end
    
    /*
    This action adds a translational component to the matrix. This action is
    mainly used in graphics programming to represent translations.

    Attribute: Author Amanda Austin

    Attribute: Parameter vector The vector to use as the translational component
    
    Attribute: Returns The matrix with the translational component
    
    Attribute: Example
    
    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 translation

    matrix:IdentityMatrix()
    translation:Set(4, 5)

    matrix:AddTranslationalComponent(translation)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action AddTranslationalComponent(Vector3 vector) returns Matrix3
        row0column2 = row0column2 + vector:GetX()
        row1column2 = row1column2 + vector:GetY()
        return me
    end

    /*
    This action translates the matrix by the given x and y translations.

    Attribute: Author Amanda Austin

    Attribute: Parameter x The x component of the translation
    Attribute: Parameter y The y component of the translation

    Attribute: Returns The matrix after translation

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    matrix:IdentityMatrix()

    matrix:Translate(4, 5)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Translate(number x, number y) returns Matrix3
        Matrix3 temporary
        temporary:row0column0 = 1
        temporary:row1column0 = 0
        temporary:row2column0 = 0
        temporary:row0column1 = 0
        temporary:row1column1 = 1
        temporary:row2column1 = 0
        temporary:row0column2 = x
        temporary:row1column2 = y
        temporary:row2column2 = 1
        Multiply(me, temporary)
        return me
    end

    /*
    This action translates the matrix by the given translation vector.

    Attribute: Author Amanda Austin

    Attribute: Parameter translation The translation vector

    Attribute: Returns The matrix after translation

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 translation

    matrix:IdentityMatrix()
    translation:Set(4, 5)

    matrix:Translate(translation)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Translate(Vector2 translation) returns Matrix3
        Matrix3 temporary
        temporary:row0column0 = 1
        temporary:row1column0 = 0
        temporary:row2column0 = 0
        temporary:row0column1 = 0
        temporary:row1column1 = 1
        temporary:row2column1 = 0
        temporary:row0column2 = translation:GetX()
        temporary:row1column2 = translation:GetY()
        temporary:row2column2 = 1
        Multiply(me, temporary)
        return me
    end

    /*
    This action rotates the matrix by the given angle in degrees.

    Attribute: Author Amanda Austin

    Attribute: Parameter degrees The angle in degrees

    Attribute: Returns The matrix after rotation

    Attribute: Example

    use Libraries.Compute.Matrix3
    
    Matrix3 matrix
    matrix:IdentityMatrix()

    matrix:Rotate(45)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Rotate(number degrees) returns Matrix3
        return RotateInRadians(math:DegreesToRadians(degrees))
    end

    /*
    This action rotates the matrix by the given angle in radians.

    Attribute: Author Amanda Austin

    Attribute: Parameter radians The angle in radians

    Attribute: Returns The matrix after rotation

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Math

    Math math
    
    Matrix3 matrix
    matrix:IdentityMatrix()

    matrix:RotateInRadians(math:pi / 4)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action RotateInRadians(number radians) returns Matrix3
        if radians = 0
            return me
        end

        number cos = math:Cosine(radians)
        number sin = math:Sine(radians)
        Matrix3 temporary
        temporary:row0column0 = cos
        temporary:row1column0 = sin
        temporary:row2column0 = 0
        temporary:row0column1 = sin * -1
        temporary:row1column1 = cos
        temporary:row2column1 = 0
        temporary:row0column2 = 0
        temporary:row1column2 = 0
        temporary:row2column2 = 1

        return Multiply(temporary)
    end

    /*
    This action scales the matrix by the given scale values in the x and y
    directions.

    Attribute: Author Amanda Austin

    Attribute: Parameter scaleX The scale in the x-direction
    Attribute: Parameter scaleY The scale in the y-direction

    Attribute: Returns The matrix after scaling

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix
    matrix:IdentityMatrix()
    
    matrix:Scale(2, 2)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Scale(number scaleX, number scaleY) returns Matrix3
        Matrix3 temporary
        temporary:row0column0 = scaleX
        temporary:row1column0 = 0
        temporary:row2column0 = 0
        temporary:row0column1 = 0
        temporary:row1column1 = scaleY
        temporary:row2column1 = 0
        temporary:row0column2 = 0
        temporary:row1column2 = 0
        temporary:row2column2 = 1
        Multiply(me, temporary)
        return me
    end

    /*
    This action scales the matrix by the given scale vector.

    Attribute: Author Amanda Austin

    Attribute: Parameter scale The scale vector

    Attribute: Returns The matrix after scaling

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 scale

    matrix:IdentityMatrix()
    scale:Set(2, 2)
    
    matrix:Scale(scale)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Scale(Vector2 scale) returns Matrix3
        Matrix3 temporary
        temporary:row0column0 = scale:GetX()
        temporary:row1column0 = 0
        temporary:row2column0 = 0
        temporary:row0column1 = 0
        temporary:row1column1 = scale:GetY()
        temporary:row2column1 = 0
        temporary:row0column2 = 0
        temporary:row1column2 = 0
        temporary:row2column2 = 1
        Multiply(me, temporary)
        return me
    end

    /*
    This action gets the translational component of the matrix.

    Attribute: Author Amanda Austin

    Attribute: Returns The translational component as a 2D vector (Vector2)

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 translation

    matrix:IdentityMatrix()

    matrix:AddTranslationalComponent(4, 5)

    translation = matrix:GetTranslation()

    output "The translation is [" + translation:GetX() + ", " + translation:GetY() + "]"

    */


    action GetTranslation returns Vector2
        Vector2 position
        position:Set(row0column2, row1column2)
        return position
    end

    /*
    This action gets the scale component of the matrix.

    Attribute: Author Amanda Austin

    Attribute: Returns The scale component as a 2D vector (Vector2)

    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 scale

    matrix:IdentityMatrix()

    matrix:SetToScaling(2, 2)

    scale = matrix:GetScale()

    output "The scale is [" + scale:GetX() + ", " + scale:GetY() + "]"

    */


    action GetScale returns Vector2
        Vector2 scale
        scale:Set(math:SquareRoot(row0column0 * row0column0 + row0column1 * row0column1), math:SquareRoot(row1column0 * row1column0 + row1column1 * row1column1))
        return scale
    end

    /*
    This action gets the rotation of the matrix in degrees.

    Attribute: Author Amanda Austin

    Attribute: Returns The rotation in degrees of the matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:IdentityMatrix()
    matrix:SetToRotation(45)

    number rotation = matrix:GetRotation()

    output "The rotation is " + rotation + " degrees."

    */


    action GetRotation returns number
        return math:InverseTangent(row1column0, row0column0) * 180 / math:pi
    end

    /*
    This action gets the rotation of the matrix in radians.

    Attribute: Author Amanda Austin

    Attribute: Returns The rotation in radians of the matrix

    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:IdentityMatrix()
    matrix:SetToRotation(45)

    number rotation = matrix:GetRotationInRadians()

    output "The rotation is " + rotation + " radians."

    */


    action GetRotationInRadians returns number
        return math:InverseTangent(row1column0, row0column0)
    end

    /*
    This action sets the scale component of the matrix to the passed scale
    value.

    Attribute: Author Amanda Austin

    Attribute: Parameter scale The scale value to set

    Attribute: Returns The matrix with the scale component set
    
    Attribute: Example

    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:IdentityMatrix()

    matrix:SetScale(2)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetScale(number scale) returns Matrix3
        row0column0 = row0column0 * scale
        row1column1 = row1column1 * scale

        return me
    end

    /*
    This action sets the scale component of the matrix to the passed scale
    vector, which is a 3D vector. The z-component of the vector is ignored.

    Attribute: Author Amanda Austin

    Attribute: Parameter scale The scale vector to set as the scale component

    Attribute: Returns The matrix with the scale component set
    
    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector3

    Matrix3 matrix
    Vector3 scale

    matrix:IdentityMatrix()
    scale:Set(2, 2, 2)

    matrix:SetScale(scale)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetScale(Vector3 scale) returns Matrix3
        row0column0 = row0column0 * scale:GetX()
        row1column1 = row1column1 * scale:GetY()
        return me
    end

    /*
    This action sets the scale component of the matrix to the passed scale
    vector, which is a 2D vector. 

    Attribute: Author Amanda Austin

    Attribute: Parameter scale The scale vector to set as the scale component

    Attribute: Returns The matrix with the scale component set
    
    Attribute: Example

    use Libraries.Compute.Matrix3
    use Libraries.Compute.Vector2

    Matrix3 matrix
    Vector2 scale

    matrix:IdentityMatrix()
    scale:Set(2, 2)

    matrix:SetScale(scale)

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action SetScale(Vector2 scale) returns Matrix3
        row0column0 = row0column0 * scale:GetX()
        row1column1 = row1column1 * scale:GetY()
        return me
    end

    /*
    This action transposes the matrix.

    Attribute: Author Amanda Austin

    Attribute: Returns The transpose of the matrix

    Attribute: Example
    
    use Libraries.Compute.Matrix3

    Matrix3 matrix

    matrix:Set(0, 1, 2, 3, 4, 5, 6, 7, 8)

    matrix:Transpose()

    number row0column0 = matrix:row0column0
    number row0column1 = matrix:row0column1
    number row0column2 = matrix:row0column2
    number row1column0 = matrix:row1column0
    number row1column1 = matrix:row1column1
    number row1column2 = matrix:row1column2
    number row2column0 = matrix:row2column0
    number row2column1 = matrix:row2column1
    number row2column2 = matrix:row2column2

    output "The new matrix is:"
    output "|" + row0column0 + ", " + row0column1 + ", " + row0column2 + "|"
    output "|" + row1column0 + ", " + row1column1 + ", " + row1column2 + "|"
    output "|" + row2column0 + ", " + row2column1 + ", " + row2column2 + "|"

    */


    action Transpose returns Matrix3
        number v01 = row1column0
        number v02 = row2column0
        number v10 = row0column1
        number v12 = row2column1
        number v20 = row0column2
        number v21 = row1column2
        row0column1 = v01
        row0column2 = v02
        row1column0 = v10
        row1column2 = v12
        row2column0 = v20
        row2column1 = v21

        return me
    end
end